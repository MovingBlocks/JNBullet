import org.apache.tools.ant.taskdefs.condition.Os
ext {
    if (Os.isFamily(Os.FAMILY_UNIX)) {
        natives = ["linux_amd64_gcc","linux_i686_gcc","linux_windows_amd64_mingw32","linux_windows_i686_mingw32"]
    }
    generatedSrcDir = 'src/generated/java'
}

// Dependencies needed for what our Gradle scripts themselves use. It cannot be included via an external Gradle file :-(
buildscript {
    repositories {
        // External libs - jcenter is Bintray and is supposed to be a superset of Maven Central, but do both just in case
        jcenter()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        dependencies {
            classpath(group: 'org.jfrog.buildinfo', name: 'build-info-extractor-gradle', version: '4.12.0')
        }
    }
}
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'idea'

group = 'org.terasology'

dependencies {
    compile group: 'org.joml', name: 'joml', version: '1.9.25'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.21'
    compile group: 'net.sf.trove4j', name: 'trove4j', version: '3.0.3'
}

sourceSets {
    main {
        java {
            srcDir generatedSrcDir
        }
    }
}

task generateSources{
    dependsOn ":swig-src:Swig"
}

compileJava.dependsOn generateSources

clean {
    // the clean task should delete the folder, because it is the
    // output folder of generateSources, but it doesn't do it.
    delete generatedSrcDir
}

repositories {
    mavenCentral()
    maven {
        url "https://plugins.gradle.org/m2/"
    }
    // MovingBlocks Artifactory instance for libs not readily available elsewhere plus our own libs
    maven {
        url "http://artifactory.terasology.org/artifactory/virtual-repo-live"
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8


task sourceJar(type: Jar) {
    description = "Create a JAR with all sources"
    from sourceSets.main.allSource
    from sourceSets.test.allSource
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    description = "Create a JAR with the JavaDoc for the java sources"
    from javadoc.destinationDir
    classifier = 'javadoc'
}
natives.each { module ->
    tasks.create(name: "native_${module}", type: Exec) {
        description = "cmake ${module} "
        executable "cmake"
        workingDir "$rootDir/build/natives/${module}"
        args "$rootDir", "-DCMAKE_TOOLCHAIN_FILE=$rootDir/toolchains/${module}.cmake", "-DCMAKE_BUILD_TYPE=Release"
        doFirst {
            mkdir "$rootDir/build/natives/${module}"
        }
        doLast {
            exec {
                workingDir "$rootDir/build/natives/${module}"
                commandLine 'make', "-j${Runtime.runtime.availableProcessors()}"
            }
        }
    }
}
task NativeBuild{
    description = "Builds Natives"
    natives.each { module ->
        dependsOn "native_${module}"
    }
}
task zipNatives(type: Zip){
    description 'Creates a zip archive that contains all TeraBullet native files'
    natives.each { module ->
        from ("$rootDir/build/natives/${module}") {
            include '*linux*'
            into 'linux'
        }

        from ("$rootDir/build/natives/${module}") {
            include '*windows*'
            into 'windows'
        }
    }

    destinationDirectory = file(buildDir)
    archiveBaseName = 'JNBullet'
}

NativeBuild.dependsOn generateSources
zipNatives.dependsOn NativeBuild

publish {
    dependsOn  sourceJar, javadocJar, zipNatives
}

// Define the artifacts we want to publish (the .pom will also be included since the Maven plugin is active)
publishing {
    publications {
        mavenJava(MavenPublication) {
            // Without this we get a .pom with no dependencies
            artifactId='JNBullet'
            groupId = 'org.terasology'

            from components.java
            artifact sourceJar
            artifact javadocJar
            artifact zipNatives

            pom.withXml {
                asNode().with {
                    appendNode('name', "JNBullet")
                    appendNode('description', "A Native Bullet Wrapper")
                    appendNode('url', "http://www.example.com/project")
                    appendNode('licenses').with {
                        appendNode('license').with {
                            appendNode('name', "The Apache License, Version 2.0")
                            appendNode('url', "http://www.apache.org/licenses/LICENSE-2.0.txt")
                        }
                    }
                    appendNode('developers').with {
                        appendNode('developer').with {
                            appendNode('id', "michaelpollind")
                            appendNode('name', "Michael Pollind")
                            appendNode('email', "mpollind@gmail.com")
                        }
                    }
                    appendNode('scm').with {
                        appendNode('connection', "https://github.com/MovingBlocks/JNBullet")
                        appendNode('developerConnection', "git@github.com:MovingBlocks/JNBullet.git")
                        appendNode('url', "https://github.com/MovingBlocks/JNBullet")
                    }
                }
            }

            repositories {
                maven {
                    name = 'TerasologyOrg'
                    url = "http://artifactory.terasology.org/artifactory/$publishRepo"
                    println "Changing PUBLISH repoKey set via Gradle property to $publishRepo"

                    if (rootProject.hasProperty("mavenUser") && rootProject.hasProperty("mavenPass")) {
                        credentials {
                            username = "$mavenUser"
                            password = "$mavenPass"
                        }
                        authentication {
                            basic(BasicAuthentication)
                        }
                    }
                }
            }
        }
    }
}

javadoc {
    failOnError = false
}
